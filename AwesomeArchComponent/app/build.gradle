apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-kapt'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.akexorcist.repositoryarchcomponents"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            postprocessing {
                removeUnusedCode false
                removeUnusedResources false
                obfuscate false
                optimizeCode false
                proguardFile 'proguard-rules.pro'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def dependencyGroup(Closure closure) {
    closure.delegate = dependencies
    return closure
}

def ui = dependencyGroup {
    // Support libraries
    implementation deps.support.app_compat
    implementation deps.support.constraint_layout
    implementation deps.support.v4
    implementation deps.support.design
    implementation deps.support.cardview
}

def glide = dependencyGroup {
    implementation deps.glide.glide
    annotationProcessor deps.glide.complier
}

def network = dependencyGroup {
    implementation deps.network.retrofit2_converter_gson
    implementation deps.network.retrofit2, { exclude group: 'com.squareup.okhttp3' }
    implementation deps.network.okhttp
    implementation deps.network.okhttp_logging_interceptor
}

def architectureComponents = dependencyGroup {
    implementation deps.lifecycle.runtime
    implementation deps.lifecycle.extensions
    kapt deps.lifecycle.compiler
    implementation deps.room.runtime
    kapt deps.room.compiler
    implementation deps.room.rxjava2
}

def rxJava = dependencyGroup {
    implementation deps.rx_android, { exclude group: "io.reactivex.rxjava2" }
    implementation deps.rxjava2
}

def kotlin = dependencyGroup {
    implementation deps.kotlin.stdlib
}

def localUnitTest = dependencyGroup {
    testImplementation deps.junit
    testImplementation deps.mockito.all
    testImplementation deps.hamcrest
    testImplementation deps.arch_core.testing
    testImplementation deps.kotlin.stdlib
    testImplementation deps.kotlin.test
}

def androidTestingSupportRunnerRule = dependencyGroup {
    androidTestCompile deps.atsl.runner
    androidTestCompile deps.atsl.rules
    androidTestCompile deps.room.testing, { exclude group: 'com.google.code.gson' }
    androidTestCompile deps.arch_core.testing
}

def androidUnitTests = dependencyGroup {
    androidTestCompile deps.junit
    androidTestCompile deps.mockito.core, { exclude group: 'net.bytebuddy' }
    androidTestCompile deps.dexmaker
}

def esspressoUiTest = dependencyGroup {
    androidTestCompile deps.espresso.core
    androidTestCompile deps.espresso.contrib
    androidTestCompile deps.espresso.intents
}

def resolveConflictsBetweenMainAndTestApk = dependencyGroup {
    androidTestCompile deps.support.annotations
    androidTestCompile deps.support.v4
    androidTestCompile deps.support.app_compat
    androidTestCompile deps.support.design
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    ui()
    glide()
    network()
    architectureComponents()
    rxJava()
    kotlin()
    localUnitTest()
    androidTestingSupportRunnerRule()
    androidUnitTests()
    esspressoUiTest()
    resolveConflictsBetweenMainAndTestApk()
}